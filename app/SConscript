import os

Import( 'env' )
Import( '*' )

if 'loudnessAnalyserLibStatic' in locals() and \
    'loudnessCorrectorLibStatic' in locals() and \
    'loudnessToolsLibStatic' in locals() and \
    'loudnessIOLibStatic' in locals():

    # Get sndfile lib name
    sndfileLib = 'sndfile'
    if env['PLATFORM'] == 'win32':
        sndfileLib = 'lib' + sndfileLib + '-1'

    ### loudness-analyser ###

    loudnessAnalyserProgram = env.Program(
        'loudness-analyser',
        Glob( 'analyser/*.cpp' ),
        LIBS = [
            loudnessAnalyserLibStatic,
            loudnessCorrectorLibStatic,
            loudnessToolsLibStatic,
            loudnessIOLibStatic,
            sndfileLib,
        ]
    )

    ### loudness-corrector ###

    loudnessCorrectorProgram = env.Program(
    'loudness-corrector',
    Glob( 'corrector/*.cpp' ),
    LIBS = [
        loudnessAnalyserLibStatic,
        loudnessCorrectorLibStatic,
        loudnessToolsLibStatic,
        loudnessIOLibStatic,
        sndfileLib,
    ]
    )

    env.Alias( 'install', env.Install( 'bin', loudnessAnalyserProgram ) )
    env.Alias( 'install', env.Install( 'bin', loudnessCorrectorProgram ) )

    ### loudness-validator ###

    # Get qt4 lib name
    qt4CoreLib = 'QtCore'
    qt4GuiLib = 'QtGui'
    if env['PLATFORM'] == 'win32':
        qt4CoreLib = qt4CoreLib + '4'
        qt4GuiLib = qt4GuiLib + '4'

    # check qt4 library
    qtEnv = env.Clone()
    conf = Configure(qtEnv)
    if conf.CheckLib(qt4CoreLib, language='cxx'):
        qtEnv = conf.Finish()

        qtEnv.Tool('qt')

        loudnessValidatorLibraries = [
            loudnessAnalyserLibStatic,
            loudnessCorrectorLibStatic,
            loudnessToolsLibStatic,
            loudnessIOLibStatic,
            sndfileLib,
        ]

        loudnessValidatorFrameworks = []

        QtLibs = [
            qt4CoreLib,
            qt4GuiLib,
        ]

        if not qtEnv['PLATFORM'] == 'darwin':
            loudnessValidatorLibraries.append( QtLibs )
        else:
            loudnessValidatorFrameworks.append( QtLibs )

        loudnessValidatorProgram = qtEnv.Program(
            'loudness-validator',
            Glob( 'LoudnessValidator/*.cpp' ),
            LIBS = loudnessValidatorLibraries,
            FRAMEWORKS = loudnessValidatorFrameworks,
        )

        qtEnv.Alias( 'install', qtEnv.Install( 'bin', loudnessValidatorProgram ) )

    else:
        print('Warning: did not find QtCore library, will not build loudness-validator app.')
        conf.Finish()

else:
    print('Warning: will not build loudness analyser/corrector/validator applications.')

if 'loudnessAnalyserLibStatic' in locals() and \
    'loudnessToolsLibStatic' in locals():

    ### media-loudness-analyser ###

    avtranscoder_root = GetOption('avtranscoder')
    avtranscoder_include = ''
    avtranscoder_lib_path = ''
    if avtranscoder_root:
        avtranscoder_include = os.path.join( avtranscoder_root, 'include' )
        avtranscoder_lib_path = os.path.join( avtranscoder_root, 'lib' )

    ffmpeg_root = GetOption('ffmpeg')
    ffmpeg_include = ''
    ffmpeg_libs_path = []
    if ffmpeg_root:
        ffmpeg_include = os.path.join( ffmpeg_root, 'include' )
        if env['PLATFORM'] == 'win32':
            ffmpeg_libs_path.append(os.path.join( ffmpeg_root, 'bin' ))
        else:
            ffmpeg_libs_path.append(os.path.join( ffmpeg_root, 'lib' ))
            ffmpeg_lib64 = os.path.join( ffmpeg_root, 'lib64' )
            if os.path.exists(ffmpeg_lib64):
                ffmpeg_libs_path.append(ffmpeg_lib64)

    # Check libavtranscoder
    avtranscoderEnv = env.Clone()

    avtranscoderEnv.Append(
        CPPPATH = [
            avtranscoder_include,
            ffmpeg_include,
        ],
        LIBPATH = [
            avtranscoder_lib_path,
            ffmpeg_libs_path,
        ],
    )

    conf = Configure(avtranscoderEnv)

    avtranscoderLib = 'avtranscoder'
    if env['PLATFORM'] == 'win32':
        avtranscoder_version = GetOption('avtranscoderVersion')
        avtranscoderLib += '-' + avtranscoder_version
    ffmpegLibs = []
    for path in ffmpeg_libs_path:
        for file in os.listdir(path):
            if file.endswith(('.so', '.dylib', '.dll')):
                ffmpegLibs.append(os.path.splitext(file)[0])

    if conf.CheckCXXHeader('AvTranscoder/common.hpp'):
        avtranscoderEnv = conf.Finish()

        mediaLoudnessAnalyserProgram = avtranscoderEnv.Program(
            'media-loudness-analyser',
            Glob( 'mediaAnalyser/*.cpp' ),
            LIBS = [
                    loudnessAnalyserLibStatic,
                    loudnessToolsLibStatic,
                    avtranscoderLib,
                    ffmpegLibs
            ]
        )

        env.Alias( 'install', env.Install( 'bin', mediaLoudnessAnalyserProgram ) )

    else:
        print('Warning: avtranscoder not found, will not build media-analyser application')
        conf.Finish()

else:
    print('Warning: will not build media loudness analyser application.')

if 'loudnessAnalyserLibStatic' in locals() and \
    'loudnessCorrectorLibStatic' in locals() and \
    'loudnessToolsLibStatic' in locals() and \
    'loudnessIOLibStatic' in locals() and \
    'admLoudnessAnalyserLib' in locals() and \
    env['PLATFORM'] != 'win32': # Windows platform not supported yet.

    ### adm-loudness-analyser ###

    adm_root = GetOption('adm')
    adm_include = ''
    adm_lib_path = ''
    if adm_root:
        adm_include = os.path.join( adm_root, 'include' )
        adm_lib_path = os.path.join( adm_root, 'lib' )

    ear_root = GetOption('ear')
    ear_include = ''
    ear_lib_path = ''
    if ear_root:
        ear_include = os.path.join( ear_root, 'include' )
        ear_lib_path = os.path.join( ear_root, 'lib' )

    bw64_include = GetOption('bw64')
    if bw64_include is None:
        bw64_include = ''

    adm_engine_root = GetOption('adm-engine')
    adm_engine_include = ''
    adm_engine_lib_path = ''
    if adm_engine_root:
        adm_engine_include = os.path.join( adm_engine_root, 'include' )
        adm_engine_lib_path = os.path.join( adm_engine_root, 'lib' )


    # Check libs
    admLoudnessEnv = env.Clone()

    admLoudnessEnv.Append(
        CPPPATH = [
            adm_include,
            ear_include,
            bw64_include,
            adm_engine_include,
        ],
        LIBPATH = [
            adm_lib_path,
            ear_lib_path,
            adm_engine_lib_path,
        ],
    )

    conf = Configure(admLoudnessEnv)

    if adm_root and ear_root and adm_engine_root and \
        conf.CheckCXXHeader('adm/adm.hpp') and \
        conf.CheckCXXHeader('ear/ear.hpp') and \
        conf.CheckCXXHeader("bw64/bw64.hpp") and \
        conf.CheckCXXHeader('adm_engine/renderer.hpp'):

        admLoudnessEnv = conf.Finish()

        admLoudnessAnalyserProgram = admLoudnessEnv.Program(
            'adm-loudness-analyser',
            Glob( 'AdmLoudnessAnalyser/*.cpp' ),
            LIBS = [
                    loudnessAnalyserLibStatic,
                    loudnessCorrectorLibStatic,
                    loudnessToolsLibStatic,
                    loudnessIOLibStatic,
                    admLoudnessAnalyserLib,
                    File(os.path.join( adm_engine_lib_path, 'libadmengine.a' )),
                    File(os.path.join( adm_lib_path, 'libadm.a' )),
                    File(os.path.join( ear_lib_path, 'libear.so' ))
            ]
        )

        env.Alias( 'install', env.Install( 'bin', admLoudnessAnalyserProgram ) )

    else:
        print('Warning: libadm, libear, libbw64 or libadmengine missing, will not build adm-loudness-analyser application')
        conf.Finish()

else:
    print('Warning: will not build ADM loudness analyser application.')
